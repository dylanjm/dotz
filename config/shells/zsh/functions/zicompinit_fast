#!/usr/bin/env zsh
# Author: NICHOLAS85
# Source: https://github.com/zdharma/zplugin-configs/blob/master/NICHOLAS85/.zplugin/plugins/_local---config-files/functions/_zpcompinit_fast

function zicompinit_fast () {
  setopt extended_glob local_options
  autoload -Uz compinit
  local zcompf="$ZINIT[ZCOMPDUMP_PATH]"
  local zcompf_a="${zcompf}.augur"

  if [[ -e "$zcompf_a" && -f "$zcompf_a"(#qN.mh+23) ]]; then
    compinit -d "$zcompf"
    touch "$zcompf_a"
  else
    compinit -C -d "$zcompf"
  fi

  # if zcompdump exists (and is non-zero), and is older than the .zwc file, then regenerate
  if [[ -s "$zcompf" && (! -s "${zcompf}.zwc" || "$zcompf" -nt "${zcompf}.zwc") ]]; then
    # since file is mapped, it might be mapped right now (current shells), so rename it then make a new one
    [[ -e "$zcompf.zwc" ]] && mv -f "$zcompf.zwc" "$zcompf.zwc.old"
    # compile it mapped, so multiple shells can share it (total mem reduction)
    # run in background
    { zcompile -M "$zcompf" && command rm -f "$zcompf.zwc.old" }&!
  fi
}

# Local Variables:
# mode: sh-script
# sh-basic-offset: 2
# End:
